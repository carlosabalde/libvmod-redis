varnishtest "Test 'auto' role"

server s1 {
   rxreq
   txresp
} -repeat 1 -start

varnish v1 -arg "-p vsl_reclen=1024" -vcl+backend {
    import redis from "${vmod_topbuild}/src/.libs/libvmod_redis.so";

    sub vcl_init {
        redis.subnets(
            masks={""});

        redis.sentinels(
            locations={""},
            period=0,
            connection_timeout=500,
            command_timeout=0);

        new db = redis.db(
            location="${redis_master1_ip}:${redis_master1_port}",
            type=auto,
            connection_timeout=500,
            connection_ttl=0,
            command_timeout=0,
            max_command_retries=0,
            shared_connections=false,
            max_connections=2,
            password="",
            sickness_ttl=0,
            max_cluster_hops=0);
        db.add_server("${redis_slave1_1_ip}:${redis_slave1_1_port}", auto);
    }

    sub vcl_deliver {
        # Fetch role (master).
        db.command("ROLE");
        db.execute(true);
        if (db.reply_is_array() &&
            db.get_array_reply_length() > 0 &&
            db.array_reply_is_string(0)) {
            set resp.http.Reply-1 = db.get_array_reply_value(0);
        }

        # Fetch role (slave).
        db.command("ROLE");
        db.execute(false);
        if (db.reply_is_array() &&
            db.get_array_reply_length() > 0 &&
            db.array_reply_is_string(0)) {
            set resp.http.Reply-2 = db.get_array_reply_value(0);
        }

        # Stats.
        set resp.http.db-stats = db.stats();
        set resp.http.db-servers-total = db.counter("servers.total");
        set resp.http.db-connections-total = db.counter("connections.total");
        set resp.http.db-commands-total = db.counter("commands.total");
    }
} -start

client c1 {
    txreq
    rxresp

    expect resp.http.Reply-1 == "master"

    expect resp.http.Reply-2 == "slave"

    expect resp.http.db-servers-total == "2"
    expect resp.http.db-connections-total == "2"
    expect resp.http.db-commands-total == "2"
} -run

varnish v1 -expect client_req == 1
