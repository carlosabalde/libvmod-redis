varnishtest "Tests password protected instances"

server s1 {
   rxreq
   txresp
} -repeat 2 -start

varnish v1 -arg "-p vsl_reclen=1024" -vcl+backend {
    import redis from "${vmod_topbuild}/src/.libs/libvmod_redis.so";

    sub vcl_init {
        new db = redis.db(
            location="${redis_master1_address}",
            connection_timeout=500,
            connection_ttl=0,
            command_timeout=0,
            command_retries=0,
            shared_contexts=false,
            max_contexts=1,
            password="",
            clustered=false,
            max_cluster_hops=0);
    }

    sub vcl_deliver {
        # SET.
        db.command("SET");
        db.push("foo");
        db.push("hello");
        db.execute();
        if (db.reply_is_status()) {
            set resp.http.Reply-1 = db.get_status_reply();
        }

        # CONFIG SET.
        db.command("CONFIG");
        db.push("SET");
        db.push("requirepass");
        db.push("s3cr3t");
        db.execute();
        if (db.reply_is_status()) {
            set resp.http.Reply-2 = db.get_status_reply();
        }
    }
} -start

varnish v2 -arg "-p vsl_reclen=1024" -vcl+backend {
    import redis from "${vmod_topbuild}/src/.libs/libvmod_redis.so";

    sub vcl_init {
        new db1 = redis.db(
            location="${redis_master1_address}",
            connection_timeout=500,
            connection_ttl=0,
            command_timeout=0,
            command_retries=0,
            shared_contexts=false,
            max_contexts=3,
            password="",
            clustered=false,
            max_cluster_hops=0);
        new db2 = redis.db(
            location="${redis_master1_address}",
            connection_timeout=500,
            connection_ttl=0,
            command_timeout=0,
            command_retries=0,
            shared_contexts=false,
            max_contexts=3,
            password="42",
            clustered=false,
            max_cluster_hops=0);
        new db3 = redis.db(
            location="${redis_master1_address}",
            connection_timeout=500,
            connection_ttl=0,
            command_timeout=0,
            command_retries=0,
            shared_contexts=false,
            max_contexts=3,
            password="s3cr3t",
            clustered=false,
            max_cluster_hops=0);
    }

    sub vcl_deliver {
        # GET (no password).
        db1.command("GET");
        db1.push("foo");
        db1.execute();
        if (db1.reply_is_error() &&
            (db1.get_error_reply() ~ "NOAUTH")) {
            set resp.http.Reply-1 = "o/";
        }

        # GET (wrong password).
        db2.command("GET");
        db2.push("foo");
        db2.execute();
        if (!db2.replied()) {
            set resp.http.Reply-2 = "o/";
        }

        # GET (right password).
        db3.command("GET");
        db3.push("foo");
        db3.execute();
        if (db3.reply_is_string()) {
            set resp.http.Reply-3 = db3.get_string_reply();
        }

        # Stats.
        set resp.http.db1-stats = db1.stats();
        set resp.http.db1-connections-total = db1.counter("connections.total");
        set resp.http.db1-connections-failed = db1.counter("connections.failed");
        set resp.http.db1-commands-total = db1.counter("commands.total");
        set resp.http.db1-commands-failed = db1.counter("commands.failed");
        set resp.http.db1-commands-error = db1.counter("commands.error");

        set resp.http.db2-stats = db2.stats();
        set resp.http.db2-connections-total = db2.counter("connections.total");
        set resp.http.db2-connections-failed = db2.counter("connections.failed");
        set resp.http.db2-commands-total = db2.counter("commands.total");
        set resp.http.db2-commands-failed = db2.counter("commands.failed");
        set resp.http.db2-commands-error = db2.counter("commands.error");

        set resp.http.db3-stats = db3.stats();
        set resp.http.db3-connections-total = db3.counter("connections.total");
        set resp.http.db3-connections-failed = db3.counter("connections.failed");
        set resp.http.db3-commands-total = db3.counter("commands.total");
        set resp.http.db3-commands-failed = db3.counter("commands.failed");
        set resp.http.db3-commands-error = db3.counter("commands.error");
    }
} -start

client c1 {
    txreq
    rxresp

    expect resp.http.Reply-1 == "OK"

    expect resp.http.Reply-2 == "OK"
} -run

client c2 -connect ${v2_sock} {
    txreq
    rxresp

    expect resp.http.Reply-1 == "o/"

    expect resp.http.Reply-2 == "o/"

    expect resp.http.Reply-3 == "hello"

    expect resp.http.db1-connections-total == "1"
    expect resp.http.db1-connections-failed == "0"
    expect resp.http.db1-commands-total == "1"
    expect resp.http.db1-commands-failed == "0"
    expect resp.http.db1-commands-error == "1"

    expect resp.http.db2-connections-total == "0"
    expect resp.http.db2-connections-failed == "1"
    expect resp.http.db2-commands-total == "0"
    expect resp.http.db2-commands-failed == "0"
    expect resp.http.db2-commands-error == "0"

    expect resp.http.db3-connections-total == "1"
    expect resp.http.db3-connections-failed == "0"
    expect resp.http.db3-commands-total == "1"
    expect resp.http.db3-commands-failed == "0"
    expect resp.http.db3-commands-error == "0"
} -run

varnish v1 -expect client_req == 1
varnish v2 -expect client_req == 1
